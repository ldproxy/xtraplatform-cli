plugins {
    id 'application'
    id 'org.graalvm.buildtools.native' version '0.10.3'
    id "com.google.osdetector" version "1.7.3"
    id "org.gretty" version "3.0.5" apply false
}

ext {
    targetos = osdetector.os.replace('osx', 'darwin')
    targetarch = osdetector.arch.replace('x86_64', 'amd64').replace('aarch_64', 'arm64')
}

group = 'de.interactive_instruments'

version = '1.0.0'

repositories {
    //mavenLocal()
    maven {
        url "https://dl.interactive-instruments.de/repository/maven-snapshots/"
    }
    maven {
        url "https://dl.interactive-instruments.de/repository/maven-releases/"
    }
    mavenCentral()
}

dependencies {
    implementation group: 'de.interactive_instruments', name: 'ldproxy-cfg', version: '4.2.0-SNAPSHOT'
    implementation group: 'org.slf4j', name: 'slf4j-nop', version: '1.7.35'
    // use this instead for debugging purposes
    //implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.35'

    // 24.x is for JDK 21
    compileOnly group: 'org.graalvm.nativeimage', name: 'svm', version: '24.1.0'
}

application {
    mainClass = 'de.ii.xtraplatform.cli.XtraCfg'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'minutes'
}

graalvmNative {
    /*agent {
        enableExperimentalPredefinedClasses = true
    }*/
    binaries.all {
        verbose = true
        fallback = false
        quickBuild = false
        richOutput = true
        //requiredVersion = '22.3'

        if (project.hasProperty('LIB')) {
            imageName = "libxtracfgjni"
            sharedLibrary = true
            buildArgs("--native-compiler-path=${project.projectDir}/../c/native-image/static-compiler-${targetos}.sh")
            buildArgs('--native-compiler-options=-fPIC')
            //buildArgs('-march=native')
            buildArgs('-O2') //TODO -O3 image is 50% larger than -O2, test -Os with JDK 23
            //buildArgs('--pgo') see https://www.graalvm.org/latest/reference-manual/native-image/optimizations-and-performance/PGO/

        } else {
            imageName = "xtracfg"
            // for statically linking xtracfg
            buildArgs('--features=de.ii.xtraplatform.cli.NativeFeature')
            buildArgs("-H:CLibraryPath=${project.projectDir}/dist")
        }

        //buildArgs("--module-path", "${buildDir}/install/ldproxy/lib")
        //buildArgs("--module", application.mainModule.get())
        //buildArgs("--enable-http")
        //buildArgs("--install-exit-handlers")
        //buildArgs("--initialize-at-run-time=org.kortforsyningen.proj")

        //runtimeArgs('/src/cfgapp')

        //jvmArgs.add('-Xmx16573624520')
        //buildArgs('-H:Log=registerResource:3')
        //buildArgs('-H:-UseServiceLoaderFeature')
        //resources.autodetect()

        if (!project.hasProperty('LIB') && targetos == 'linux') {
            buildArgs('-H:+StaticExecutableWithDynamicLibC')
        }
        if (targetos == 'darwin') {
            buildArgs('--native-compiler-options=-Wl,-framework,Security')
        }

        // for jackson???
        //buildArgs('-H:+PrintClassInitialization')
        //buildArgs("--allow-incomplete-classpath")


        buildArgs("--add-exports=org.graalvm.nativeimage.builder/com.oracle.svm.core.jdk=ALL-UNNAMED")
        buildArgs("--add-exports=org.graalvm.nativeimage.builder/com.oracle.svm.hosted=ALL-UNNAMED")
        buildArgs("--add-exports=org.graalvm.nativeimage.builder/com.oracle.svm.hosted.c=ALL-UNNAMED")
    }
    toolchainDetection = false
}

tasks.register('nativeSync', Sync) {
    from "${project.buildDir}/native/nativeCompile"
    into '../c/build'
}

tasks.register('os') {
    doLast {
        println targetos + "-" + targetarch
    }
}

if (project.hasProperty('LIB')) {
    tasks.nativeCompile.finalizedBy tasks.nativeSync
}

if (!project.hasProperty('CI')) {
    apply from: 'dev.gradle'
}
